/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.Algebra.Homology.ExactSequence
import Mathlib.Tactic.Linarith
import Mathlib.CategoryTheory.Abelian.Exact

/-!
# Additive Functions from an abelian Category

If `G` is an commutative additive group and `ğ’` an abelian category, then an additive function with
value in `G` is a function `Î¼ : ğ’ â†’ G` such that whenever `0 â†’ A â†’ B â†’ C â†’ 0` is short exact,
we have `Î¼(B) = Î¼(A) + Î¼(C)`.

## Main results

- `Î¼(0) = 0`
- `Î¼(A) = Î¼(B)` if `A â‰… B`
- if `f : A âŸ¶ B`, then `Î¼ (kernel f) + Î¼ (image f) = Î¼ A` and `Î¼ (image f) + Î¼ (cokernel f) = Î¼ B`
- if `Aâ‚€ â†’ Aâ‚ â†’ Aâ‚‚ â†’ Aâ‚ƒ â†’ Aâ‚„ â†’ Aâ‚…` is exact, then
  `Î¼(Aâ‚€) - Î¼(Aâ‚) + Î¼(Aâ‚‚) - Î¼(Aâ‚ƒ) + Î¼(Aâ‚„) - Î¼(Aâ‚…) = Î¼ (ker fâ‚€) - Î¼ (coker fâ‚„)`.

-/

open CategoryTheory CategoryTheory.Limits

universe u u' v v' w

variable (ğ’ : Type u) [Category.{v} ğ’] (ğ’Ÿ : Type u') [Category.{v'} ğ’Ÿ]
variable (G : Type w) [AddCommGroup G]
variable [Abelian ğ’] [Abelian ğ’Ÿ]

open ZeroObject

/--
A function `Î» : ğ’ â†’ â„¤` is additive precisely when `Î» B = Î» A + Î» C` for every short exact sequence
`s := 0 --> A --> B --> C --> 0`.
-/
structure AdditiveFunction where
  /--
  A function `Î» : ğ’ â†’ â„¤` is additive precisely when `Î» B = Î» A + Î» C` for every short exact
  sequence `s := 0 --> A --> B --> C --> 0`.
  -/
  toFun : ğ’ â†’ G
  /--
  A function `Î» : ğ’ â†’ â„¤` is additive precisely when `Î» B = Î» A + Î» C` for every short exact
  sequence `s := 0 --> A --> B --> C --> 0`.
  -/
  additive (s : ShortComplex ğ’) (hs : s.ShortExact) : toFun s.Xâ‚ + toFun s.Xâ‚ƒ = toFun s.Xâ‚‚

@[inherit_doc]
notation ğ’ "âŸ¹+" G => AdditiveFunction ğ’ G

namespace AdditiveFunction

variable {ğ’ G}
variable (Î¼ : ğ’ âŸ¹+ G)

private lemma ext' {Î± Î² : ğ’ âŸ¹+ G} (h : Î±.toFun = Î².toFun) : Î± = Î² := by
  cases Î±; cases Î²; rwa [mk.injEq]

instance : DFunLike (AdditiveFunction ğ’ G) ğ’ (fun _ â†¦ G) where
  coe Î¼ := Î¼.toFun
  coe_injective' _ _ h := AdditiveFunction.ext' h

@[ext] lemma ext {Î± Î² : ğ’ âŸ¹+ G} (h : âˆ€ x, Î± x = Î² x) : Î± = Î² := by
  apply ext'; ext; apply h

lemma map_zero : Î¼ 0 = 0 := by
  let s : ShortComplex ğ’ :=
  { Xâ‚ := 0
    Xâ‚‚ := 0
    Xâ‚ƒ := 0
    f := 0
    g := 0
    zero := by aesop_cat }
  have hs : s.Exact := by
    rw [ShortComplex.exact_iff_mono]
    simp only [s]
    Â· infer_instance
    Â· rfl
  replace hs : s.ShortExact := âŸ¨hsâŸ©
  have : Î¼ 0 + Î¼ 0 = Î¼ 0 := Î¼.additive s hs
  aesop

lemma eq_of_iso {x y : ğ’} (e : x â‰… y) : Î¼ x = Î¼ y := by
  let s : ShortComplex ğ’ :=
  { Xâ‚ := x
    Xâ‚‚ := y
    Xâ‚ƒ := 0
    f := e.hom
    g := 0
    zero := by aesop_cat }
  have hs : s.Exact := by
    rw [ShortComplex.exact_iff_epi]
    Â· infer_instance
    Â· rfl
  replace hs : s.ShortExact := âŸ¨hsâŸ©
  have : Î¼ x + Î¼ 0 = Î¼ y := Î¼.additive s hs
  rwa [map_zero, add_zero] at this

section

variable {A B : ğ’} (f : A âŸ¶ B)

private noncomputable abbrev sc1 : ShortComplex ğ’ where
  Xâ‚ := kernel f
  Xâ‚‚ := A
  Xâ‚ƒ := image f
  f := kernel.Î¹ _
  g := factorThruImage f
  zero := zero_of_comp_mono (image.Î¹ f) <| by
    rw [Category.assoc, image.fac, kernel.condition]

private lemma sc1_exact : sc1 f |>.Exact := by
  simp only [sc1]
  apply ShortComplex.exact_of_f_is_kernel

  fapply KernelFork.IsLimit.ofÎ¹
  Â· intro x g h
    refine kernel.lift _ g ?_
    rw [â† image.fac f, â† Category.assoc, h]
    simp only [zero_comp]
  Â· intro x g h
    simp only [kernel.lift_Î¹]
  Â· intro x g h g' h'
    ext
    simpa

private lemma sc1_shortExact : sc1 f |>.ShortExact := âŸ¨sc1_exact _âŸ©

private noncomputable abbrev sc2 : ShortComplex ğ’ where
  Xâ‚ := image f
  Xâ‚‚ := B
  Xâ‚ƒ := cokernel f
  f := image.Î¹ _
  g := cokernel.Ï€ _
  zero := by aesop_cat

private lemma sc2_exact : sc2 f |>.Exact := by
  apply ShortComplex.exact_of_g_is_cokernel
  simp only
  fapply CokernelCofork.IsColimit.ofÏ€
  Â· intro x g h
    refine cokernel.desc _ g ?_
    rw [â† image.fac f, Category.assoc, h]
    simp
  Â· aesop_cat
  Â· intros x g h g' h'
    aesop_cat

private lemma sc2_shortExact : sc2 f |>.ShortExact := âŸ¨sc2_exact _âŸ©

lemma eq_apply_kernel_add_apply_image : Î¼ (kernel f) + Î¼ (image f) = Î¼ A :=
  Î¼.additive _ <| sc1_shortExact f

lemma eq_apply_image_add_apply_cokernel : Î¼ (image f) + Î¼ (cokernel f) = Î¼ B :=
  Î¼.additive _ <| sc2_shortExact f

lemma apply_kernel_sub_apply_cokernel_eq_apply_src_sub_apply_tgt :
    Î¼ (kernel f) - Î¼ (cokernel f) = Î¼ A - Î¼ B := by
  have eq1 := congr_argâ‚‚ (Â· - Â·) (Î¼.eq_apply_kernel_add_apply_image f)
    (Î¼.eq_apply_image_add_apply_cokernel f)
  simp only at eq1
  rw [â† eq1]
  abel

end

section ShortComplex

variable (s : ShortComplex ğ’) (hs : s.Exact)

private noncomputable def imageIsoKernel : image s.f â‰… kernel s.g :=
  calc image s.f
    _ â‰… imageSubobject s.f := imageSubobjectIso _ |>.symm
    _ â‰… kernelSubobject s.g :=
      letI : IsIso (imageToKernel s.f s.g s.zero) := by
        rwa [â† ShortComplex.exact_iff_isIso_imageToKernel]
      asIso (imageToKernel s.f s.g _)
    _ â‰… kernel s.g := kernelSubobjectIso _

include hs in
lemma apply_shortComplex_of_exact : Î¼ (kernel s.f) - Î¼ (image s.g) = Î¼ s.Xâ‚ - Î¼ s.Xâ‚‚ := by
  have eq1 : Î¼ (kernel s.f) + Î¼ (image s.f) - (Î¼ (kernel s.g) + Î¼ (image s.g)) = Î¼ s.Xâ‚ - Î¼ s.Xâ‚‚ :=
    congr_argâ‚‚ (Â· - Â·)
      (Î¼.eq_apply_kernel_add_apply_image s.f)
      (Î¼.eq_apply_kernel_add_apply_image s.g)
  rw [Î¼.eq_of_iso (imageIsoKernel s hs)] at eq1
  rwa [add_comm (Î¼ (kernel s.g)), add_sub_add_right_eq_sub] at eq1

lemma apply_shortComplex_of_exact' : Î¼ (kernel s.g) - Î¼ (cokernel s.g) = Î¼ s.Xâ‚‚ - Î¼ s.Xâ‚ƒ :=
  Î¼.apply_kernel_sub_apply_cokernel_eq_apply_src_sub_apply_tgt s.g

end ShortComplex

section ComposableArrows

section arbitrary_length

variable {N : â„•} (S : ComposableArrows ğ’ N) (hS : S.Exact)

local notation "ker_" m => kernel (S.map' m (m + 1))
local notation "im_" m => image (S.map' m (m + 1))

@[simps!]
private noncomputable def im_eq_ker_succ (n : â„•) (hn : n + 2 â‰¤ N := by omega) :
    (image (S.map' n (n + 1))) â‰… kernel (S.map' (n + 1) (n + 2)) :=
  (imageSubobjectIso (S.map' n (n + 1))).symm â‰ªâ‰«
    @asIso _ _ _ _ (imageToKernel (S.map' n (n + 1)) (S.map' (n + 1) (n + 2)) <|
        hS.toIsComplex.zero n) (by
        let S' : ShortComplex ğ’ := S.sc hS.toIsComplex n
        change IsIso (imageToKernel S'.f S'.g S'.zero)
        rw [â† ShortComplex.exact_iff_isIso_imageToKernel]
        exact hS.exact _) â‰ªâ‰«
  kernelSubobjectIso (S.map' (n + 1) (n + 2))

include hS in
lemma apply_image_eq_apply_ker_succ (n : â„•) (hn : n + 2 â‰¤ N) : Î¼ (im_ n) = Î¼ (ker_ (n + 1)) :=
  Î¼.eq_of_iso (im_eq_ker_succ S hS n hn)

include hS in
lemma apply_sub_apply_succ (n : â„•) (hn : n + 3 â‰¤ N) :
    Î¼ (S.obj' n) - Î¼ (S.obj' (n + 1)) =
    Î¼ (ker_ n) - Î¼ (ker_ (n + 2)) := by
  have eq0 : Î¼ (S.obj' n) - Î¼ (S.obj' (n + 1)) = Î¼ (ker_ n) - Î¼ (im_ (n + 1)) :=
    Î¼.apply_shortComplex_of_exact (S.sc hS.toIsComplex n) (hS.exact _) |>.symm
  rw [apply_image_eq_apply_ker_succ (hS := hS)] at eq0
  exact eq0

include hS in
lemma apply_eq_apply_image_add_apply_image
    (n : â„•) (hn1 : 1 â‰¤ n := by omega) (hn2 : n + 1 â‰¤ N := by omega) :
    Î¼ (S.obj' n) = Î¼ (image (S.map' (n - 1) n)) + Î¼ (image (S.map' n (n + 1))) := by
  let sc : ShortComplex ğ’ :=
  { Xâ‚ := image (S.map' (n - 1) n)
    Xâ‚‚ := S.obj' n
    Xâ‚ƒ := image (S.map' n (n + 1))
    f := image.Î¹ _
    g := factorThruImage (S.map' _ _)
    zero := by
      refine zero_of_comp_mono (image.Î¹ _) ?_
      rw [Category.assoc, image.fac]
      refine image.ext _ ?_
      rw [image.fac_assoc, comp_zero]

      have eq1 :
          S.map' (n - 1) (n - 1 + 1) â‰« S.map' (n - 1 + 1) (n - 1 + 2) â‰«
          S.map' (n - 1 + 2) (n + 1) = 0 := by
        rw [â† Category.assoc, hS.toIsComplex.zero (n - 1), zero_comp]
      simp only [â† S.map_comp, homOfLE_comp, â† eq1] }

  have sc_exact : sc.Exact := by
    have e1 := hS.exact' (n - 1) n (n + 1)
    rw [ShortComplex.exact_iff_image_eq_kernel] at e1 âŠ¢
    simp only [ComposableArrows.obj', Int.reduceNeg, id_eq, Int.Nat.cast_ofNat_Int,
      ComposableArrows.map', homOfLE_leOfHom] at e1 âŠ¢
    convert e1 using 1
    Â· exact imageSubobject_mono _
    Â· generalize_proofs _ _ _ h
      simp_rw [â† image.fac (S.map <| homOfLE h)]
      rw [kernelSubobject_comp_mono]

  have sc_shortExact : sc.ShortExact := âŸ¨sc_exactâŸ©
  exact Î¼.additive _ sc_shortExact |>.symm

include hS in
lemma apply_eq_apply_kernel_add_apply_kernel
    (n : â„•) (hn : n + 2 â‰¤ N) :
    Î¼ (S.obj' n) = Î¼ (kernel (S.map' n (n + 1))) + Î¼ (kernel (S.map' (n + 1) (n + 2))) := by
  let sc : ShortComplex ğ’ :=
  { Xâ‚ := kernel (S.map' n (n + 1))
    Xâ‚‚ := S.obj' n
    Xâ‚ƒ := kernel (S.map' (n + 1) (n + 2))
    f := kernel.Î¹ _
    g := kernel.lift _ (S.map' _ _) <| hS.toIsComplex.zero n
    zero := zero_of_comp_mono (kernel.Î¹ _) <| by simp }

  have sc_exact : sc.Exact := by
    apply ShortComplex.exact_of_f_is_kernel
    simp only [id_eq, Int.reduceNeg, Int.Nat.cast_ofNat_Int, Nat.cast_ofNat, ComposableArrows.map',
      homOfLE_leOfHom, ComposableArrows.obj', sc]
    fapply KernelFork.IsLimit.ofÎ¹
    Â· intro x g h
      exact kernel.lift _ g <| by simpa using h =â‰« kernel.Î¹ _
    Â· intro x g h
      simp
    Â· intro x g h g' h'
      ext
      simpa

  have sc_shortExact : sc.ShortExact := by
    refine .mk' sc_exact equalizer.Î¹_mono ?_
    change Epi (kernel.lift _ _ _)

    suffices eq0 :
      (kernel.lift _ (S.map' n (n + 1)) <| hS.toIsComplex.zero n) =
      factorThruImage _ â‰« (im_eq_ker_succ S hS n).hom by rw [eq0]; exact epi_comp _ _

    ext
    rw [im_eq_ker_succ_hom (n := n), kernel.lift_Î¹, Category.assoc, Category.assoc, Category.assoc,
      kernelSubobject_arrow, imageToKernel_arrow, imageSubobject_arrow', image.fac]

  exact Î¼.additive _ sc_shortExact |>.symm

end arbitrary_length

section length6

variable (S : ComposableArrows ğ’ 5) (hS : S.Exact)

local notation "Î¼_" n => Î¼ (S.obj' n)

include hS in
lemma alternating_apply_aux_of_length6 :
    (Î¼_ 0) - (Î¼_ 1) + (Î¼_ 2) - (Î¼_ 3) + (Î¼_ 4) - (Î¼_ 5) =
    (Î¼ (kernel (S.map' 0 1)) - Î¼ (kernel (S.map' 4 5))) + (Î¼_ 4) - (Î¼_ 5) := by
  rw [show (Î¼_ 0) - (Î¼_ 1) + (Î¼_ 2) - (Î¼_ 3) + (Î¼_ 4) - (Î¼_ 5) =
    ((Î¼_ 0) - (Î¼_ 1)) + ((Î¼_ 2) - (Î¼_ 3)) + ((Î¼_ 4) - (Î¼_ 5)) by abel]

  rw [apply_sub_apply_succ (hS := hS) (n := 0), apply_sub_apply_succ (hS := hS) (n := 2)]
  abel

include hS in
lemma alternating_sum_apply_of_length6 :
    (Î¼_ 0) - (Î¼_ 1) + (Î¼_ 2) - (Î¼_ 3) + (Î¼_ 4) - (Î¼_ 5) =
    Î¼ (kernel (S.map' 0 1)) - Î¼ (cokernel (S.map' 4 5)) := by
  rw [Î¼.alternating_apply_aux_of_length6 (hS := hS)]
  have eq0 : _ = Î¼ (S.obj' 4) - Î¼ (S.obj' 5) :=
    Î¼.apply_shortComplex_of_exact' (S.sc hS.toIsComplex 3)
  rw [add_sub_assoc, â† eq0]
  simp only [Int.ofNat_eq_coe, Int.Nat.cast_ofNat_Int, id_eq, Nat.cast_ofNat, Fin.zero_eta,
    Fin.mk_one, ComposableArrows.map', sub_add_sub_cancel]

include hS in
lemma alternating_sum_apply_eq_zero_of_zero_zero_of_length6_aux
    (left_zero : IsZero S.left) (right_zero : IsZero S.right) :
    (Î¼_ 0) - (Î¼_ 1) + (Î¼_ 2) - (Î¼_ 3) + (Î¼_ 4) - (Î¼_ 5) = 0 := by
  rw [alternating_sum_apply_of_length6 (hS := hS)]
  rw [show Î¼ (kernel (S.map' 0 1)) = 0 from ?_, show Î¼ (cokernel (S.map' 4 5)) = 0 from ?_,
    sub_zero]
  Â· rw [Î¼.eq_of_iso, Î¼.map_zero]
    rw [show ComposableArrows.map' S 4 5 = 0 from IsZero.eq_zero_of_tgt right_zero _]
    exact cokernelZeroIsoTarget â‰ªâ‰« right_zero.isoZero
  Â· rw [Î¼.eq_of_iso, Î¼.map_zero]
    rw [show ComposableArrows.map' S 0 1 = 0 from IsZero.eq_zero_of_src left_zero _]
    exact kernelZeroIsoSource â‰ªâ‰« left_zero.isoZero

include hS in
lemma alternating_sum_apply_eq_zero_of_zero_zero_of_length6
    (left_zero : IsZero S.left) (right_zero : IsZero S.right) :
    - (Î¼_ 1) + (Î¼_ 2) - (Î¼_ 3) + (Î¼_ 4) = 0 := by
  refine Eq.trans ?_ <|
    Î¼.alternating_sum_apply_eq_zero_of_zero_zero_of_length6_aux (hS := hS)
      S left_zero right_zero
  rw [show (Î¼_ 0) = 0 from (Î¼.eq_of_iso <| IsZero.iso left_zero <| isZero_zero _).trans Î¼.map_zero]
  rw [show (Î¼_ 5) = 0 from (Î¼.eq_of_iso <| IsZero.iso right_zero <| isZero_zero _).trans Î¼.map_zero]
  rw [zero_sub, sub_zero]

include hS in
lemma alternating_sum_apply_eq_zero_of_zero_zero_of_length6'
    (left_zero : IsZero S.left) (right_zero : IsZero S.right) :
    (Î¼_ 1) - (Î¼_ 2) + (Î¼_ 3) - (Î¼_ 4) = 0 := by
  have eq0 := congr_arg (-Â·) <|
    Î¼.alternating_sum_apply_eq_zero_of_zero_zero_of_length6 (hS := hS) S left_zero right_zero
  dsimp only [id_eq, Int.reduceNeg, Int.Nat.cast_ofNat_Int, Nat.cast_ofNat, Int.reduceAdd,
    Int.reduceSub, ComposableArrows.obj', Nat.reduceAdd, Fin.mk_one, Fin.reduceFinMk] at eq0
  rw [neg_zero] at eq0
  rw [â† eq0]
  abel

end length6

end ComposableArrows

section AddCommGroup

instance add : Add (ğ’ âŸ¹+ G) where
  add Î± Î² :=
  { toFun := Î± + Î²
    additive := fun s hs â†¦ by
      have eq0 : Î± _ + Î± _ + (Î² _ + Î² _) = Î± _ + Î² _ :=
        congr_argâ‚‚ (Â· + Â·) (Î±.additive _ hs) (Î².additive _ hs)
      simp only [Pi.add_apply] at eq0 âŠ¢
      rw [â† eq0]
      abel }

@[simp] lemma add_apply (Î± Î² : ğ’ âŸ¹+ G) (x) : (Î± + Î²) x = Î± x + Î² x := rfl

instance neg : Neg (ğ’ âŸ¹+ G) where
  neg Î¼ :=
  { toFun := - Î¼
    additive := fun s hs â†¦ by
      have eq0 : - (Î¼ _ + Î¼ _) = - Î¼ _ := congr_arg (- Â·) (Î¼.additive _ hs)
      simp only [Pi.neg_apply] at eq0 âŠ¢
      rw [â† eq0]
      abel }

@[simp] lemma neg_apply (Î¼ : ğ’ âŸ¹+ G) (x) : (-Î¼) x = - (Î¼ x) := rfl

instance zero : Zero (ğ’ âŸ¹+ G) where
  zero :=
  { toFun := 0
    additive := fun _ _ â†¦ show 0 + 0 = 0 by simp }

@[simp] lemma zero_apply (x) : (0 : ğ’ âŸ¹+ G) x = 0 := rfl

instance addSemigroup : AddSemigroup (ğ’ âŸ¹+ G) where
  add_assoc Î± Î² Î³ := ext fun x â†¦ by simp [add_assoc]

instance addZeroClass : AddZeroClass (ğ’ âŸ¹+ G) where
  zero_add _ := ext fun _ â†¦ by simp
  add_zero _ := ext fun _ â†¦ by simp

instance addMonoid : AddMonoid (ğ’ âŸ¹+ G) where
  __ := addSemigroup
  __ := addZeroClass
  nsmul := nsmulRec
  nsmul_zero _ := by simp only [nsmulRec]
  nsmul_succ _ _ := by simp only [nsmulRec]

instance addCommMonoid : AddCommMonoid (ğ’ âŸ¹+ G) where
  __ := addMonoid
  add_comm _ _ := ext fun _ â†¦ by simp [add_comm]

instance : AddCommGroup (ğ’ âŸ¹+ G) where
  __ := addCommMonoid
  neg_add_cancel _ := ext fun _ â†¦ by simp
  zsmul := zsmulRec

end AddCommGroup

section equivalence

variable {ğ’Ÿ}
variable (e : ğ’ â‰Œ ğ’Ÿ)

/--
pushforward of an additive function along a category equivalence
-/
@[simps]
def pushforward : ğ’Ÿ âŸ¹+ G where
  toFun x := Î¼ (e.inverse.obj x)
  additive _ h := Î¼.additive _ (h.map_of_exact e.inverse)

end equivalence

end AdditiveFunction
