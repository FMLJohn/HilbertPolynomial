/-
Copyright (c) 2024 Fangming Li. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fangming Li
-/
import HilbertPolynomial.HilbertSerre.Theorem

import Mathlib.RingTheory.Polynomial.HilbertPoly

/-!
# Hilbert polynomials

Remember the assumptions in the file `HilbertPolynomial/HilbertSerre/Theorem.lean`:
`universe u`
`variable {A M : Type u}`
`variable [CommRing A] [noetherian_ring : IsNoetherianRing A]`
`variable [AddCommGroup M] [Module A M] [finite_module : Module.Finite A M]`
`variable (ğ’œ : â„• â†’ AddSubgroup A) (â„³ : â„• â†’ AddSubgroup M)`
`variable [GradedRing ğ’œ] [DirectSum.Decomposition â„³] [SetLike.GradedSMul ğ’œ â„³]`
`variable (Î¼ : (FGModuleCat (ğ’œ 0)) âŸ¹+ â„¤)`
`variable (S : generatingSetOverBaseRing ğ’œ)`

This file inherits all the above settings. With an additional assumption
`hS : (i : S.toFinset) â†’ S.deg i.2 = 1`, the main things achieved in this file are:
1. formalising the Hilbert polynomial `HilbertSerre.hilbertPolynomial â„³ Î¼ S : â„š[X]`;
2. proving that for any large enough `n : â„•`, the value of the additive function `Î¼` at `â„³ n`
   is equal to the value of the Hilbert polynomial at `n`;
3. showing that the polynomial `h` satisfying the above property (i.e. for any large enough
   `n : â„•`, the value of `Î¼` at `â„³ n` equals the value of `h` at `n`) is unique;
4. proving a theorem `HilbertSerre.natDegree_hilbertPolynomial`, which tells us the specific
   degree of any non-zero Hilbert polynomial.
-/

universe u
variable {A M : Type u}
variable [CommRing A] [noetherian_ring : IsNoetherianRing A]
variable [AddCommGroup M] [Module A M] [finite_module : Module.Finite A M]
variable (ğ’œ : â„• â†’ AddSubgroup A) (â„³ : â„• â†’ AddSubgroup M)
variable [GradedRing ğ’œ] [DirectSum.Decomposition â„³] [SetLike.GradedSMul ğ’œ â„³]
variable (Î¼ : (FGModuleCat (ğ’œ 0)) âŸ¹+ â„¤)
variable (S : generatingSetOverBaseRing ğ’œ) (hS : (i : S.toFinset) â†’ S.deg i.2 = 1)

open AdditiveFunction BigOperators generatingSetOverBaseRing Polynomial PowerSeries

namespace HilbertSerre

variable {ğ’œ}

/--
Remember the Hilbert Serre Theorem (`hilbert_serre`), which says that there exists some
`p : â„¤[X]` such that `Î¼.poincareSeries ğ’œ â„³ = p â€¢ S.polesâ»Â¹`. This definition is the
polynomial `p` guaranteed by `hilbert_serre`.
-/
noncomputable def numeratorPolynomial : Polynomial â„¤ := (hilbert_serre ğ’œ â„³ Î¼ S).choose

theorem numeratorPolynomial_mul_inv_poles_eq_poincareSeries :
    (numeratorPolynomial â„³ Î¼ S).toPowerSeries * S.polesâ»Â¹ = Î¼.poincareSeries ğ’œ â„³ :=
  (hilbert_serre ğ’œ â„³ Î¼ S).choose_spec.symm

/--
The Hilbert polynomial, i.e. the polynomial such that for any `n : â„•` which
is big enough, the value of `Î¼` at `â„³ n` is equal to its value at `n`.
-/
noncomputable def hilbertPolynomial : Polynomial â„š :=
  (Polynomial.map (Int.castRingHom â„š) (numeratorPolynomial â„³ Î¼ S)).hilbertPoly S.toFinset.card

variable {S}

include hS in
/--
The key property of the Hilbert polynomial, i.e. for any `n : â„•` that is large enough,
the value of `Î¼` at `â„³ n` is equal to the value of the Hilbert polynomial at `n`.
-/
theorem AdditiveFunction_eq_hilbertPolynomial_eval
    {n : â„•} (hn : (numeratorPolynomial â„³ Î¼ S).natDegree < n) :
    Î¼.toFun (FGModuleCat.of (ğ’œ 0) (â„³ n)) = (hilbertPolynomial â„³ Î¼ S).eval (n : â„š) := by
  change Int.castRingHom â„š _ = _
  have heq1 : Î¼.toFun (FGModuleCat.of (ğ’œ 0) (â„³ n)) = coeff â„¤ n (Î¼.poincareSeries ğ’œ â„³) := by
    rw [poincareSeries, coeff_mk]
    rfl
  have heq2 : (PowerSeries.map (Int.castRingHom â„š)) (numeratorPolynomial â„³ Î¼ S) =
      Polynomial.map (Int.castRingHom â„š) (numeratorPolynomial â„³ Î¼ S) := by
    ext n
    simp only [PowerSeries.coeff_map, coeff_coe, eq_intCast, polynomial_map_coe]
  have heq3 : map (Int.castRingHom â„š) â†‘S.polesâ»Â¹ = invOneSubPow â„š S.toFinset.card := by
    simp only [poles, hS, pow_one, Finset.prod_const, Finset.card_attach, Units.inv_mk,
      invOneSubPow_eq_inv_one_sub_pow, inv_pow]
    refine Units.eq_inv_of_mul_eq_one_right ?_
    Â· have : ((1 - PowerSeries.X) ^ S.toFinset.card : â„šâŸ¦XâŸ§) = (PowerSeries.map (Int.castRingHom â„š))
          ((1 - PowerSeries.X) ^ S.toFinset.card : â„¤âŸ¦XâŸ§) := by
        simp only [map_pow, map_sub, map_one, PowerSeries.map_X]
      rw [Units.val_pow_eq_pow_val, Units.val_mkOfMulEqOne, this, â† map_mul]
      simp only [map_pow, map_sub, constantCoeff_X, sub_zero, one_pow, Units.val_one, invOfUnit_mul,
        map_one]
  rw [heq1, hilbertPolynomial, â† numeratorPolynomial_mul_inv_poles_eq_poincareSeries â„³ Î¼ S,
    â† PowerSeries.coeff_map, map_mul, heq2, heq3, coeff_mul_invOneSubPow_eq_hilbertPoly_eval]
  exact lt_of_le_of_lt natDegree_map_le hn

include hS in
/--
The Hilbert polynomial is unique. In other words, for any `h : â„š[X]`, if `h` satisfies the key
property of the Hilbert polynomial (i.e. for any large enough `n : â„•`, the value of `Î¼` at `â„³ n`
equals the value of `h` at `n`), then `h` is the Hilbert polynomial itself.
-/
theorem exists_unique_hilbertPolynomial :
    âˆƒ! h : Polynomial â„š, âˆƒ N : â„•, âˆ€ n > N,
    Î¼.toFun (FGModuleCat.of (ğ’œ 0) (â„³ n)) = h.eval (n : â„š) := by
  use hilbertPolynomial â„³ Î¼ S; constructor
  Â· use (numeratorPolynomial â„³ Î¼ S).natDegree
    exact fun n hn => AdditiveFunction_eq_hilbertPolynomial_eval â„³ Î¼ hS hn
  Â· rintro h âŸ¨N, hhNâŸ©
    apply eq_of_infinite_eval_eq h (hilbertPolynomial â„³ Î¼ S)
    apply ((Set.Ioi_infinite (max N (numeratorPolynomial â„³ Î¼ S).natDegree)).image
      Nat.cast_injective.injOn).mono
    rintro x âŸ¨n, hn, rflâŸ©
    simp only [Set.mem_Ioi, sup_lt_iff, Set.mem_setOf_eq] at hn âŠ¢
    rw [â† AdditiveFunction_eq_hilbertPolynomial_eval â„³ Î¼ hS hn.2, hhN n hn.1]

/--
This theorem tells us the specific degree of any non-zero Hilbert polynomial.
-/
theorem natDegree_hilbertPolynomial (hhP : hilbertPolynomial â„³ Î¼ S â‰  0) :
    (hilbertPolynomial â„³ Î¼ S).natDegree =
    S.toFinset.card - (numeratorPolynomial â„³ Î¼ S).rootMultiplicity 1 - 1 := by
  simp only [hilbertPolynomial, Polynomial.natDegree_hilbertPoly_of_ne_zero hhP,
    eq_rootMultiplicity_map (Int.castRingHom â„š).injective_int, eq_intCast, Int.cast_one]

end HilbertSerre
